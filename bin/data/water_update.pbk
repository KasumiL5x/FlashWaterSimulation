<languageVersion: 1.0;>

kernel water_update
<
	namespace: "dgreen";
	vendor: "dgreen";
	version: 1;
>
{
	input image4 source;
	output pixel4 destination;

	void evaluatePixel()
	{
		float2 coord = outCoord();
		
		// Delta size of one pixel.
		float2 dx = float2(pixelSize(source).x, 0.0);
		float2 dy = float2(0.0, pixelSize(source).y);
		// Average of surrounding heights.
		float left  = sampleNearest(source, coord - dx).r;
		float right = sampleNearest(source, coord + dx).r;
		float up    = sampleNearest(source, coord - dy).r;
		float down  = sampleNearest(source, coord + dy).r;
		float avg = (left + right + up + down) * 0.25;
		// 
		destination = sampleNearest(source, coord).xyzw;
		destination.y += (avg - destination.x) * 2.0;
		destination.y *= 0.985;//0.995;
		destination.x += destination.y;
		
		destination.x *= 0.99;
	}
}
